    SUBROUTINE ITER_U(IN,INM,INA,INB,INC,IND)
    
    USE COMDAT_SHARED
    
    IMPLICIT NONE
    SAVE
	
	INCLUDE 'mpif.h'
    
	!   DEFINE ARGUMENT VARIABLES
	
	DOUBLE PRECISION, INTENT(INOUT)	:: IN (IS:IE,JS:JE,KS:KE)
	DOUBLE PRECISION, INTENT(IN)	:: INM(IS:IE,JS:JE,KS:KE)
	DOUBLE PRECISION, INTENT(IN)	:: INA(IS:IE,JS:JE,KS:KE)
	DOUBLE PRECISION, INTENT(IN)	:: INB(IS:IE,JS:JE,KS:KE)
	DOUBLE PRECISION, INTENT(IN)	:: INC(IS:IE,JS:JE,KS:KE)
	DOUBLE PRECISION, INTENT(IN)	:: IND(IS:IE,JS:JE,KS:KE)
	

	!   TRI-DIAGONAL COMPONENTS OF LINEAR MATRIX EQUATION
	
	IF(UIN.EQ.1.D0) THEN
    AP(IS1:IE1,JS1:JE1,KS1:KE1) = 2.D0*RE*DJAC(IS1:IE1,JS1:JE1,KS1:KE1)/DTIME				&
    							+ DE(IS1:IE1,JS1:JE1,KS1:KE1)+DW(IS1:IE1,JS1:JE1,KS1:KE1)	&
								+ DN(IS1:IE1,JS1:JE1,KS1:KE1)+DS(IS1:IE1,JS1:JE1,KS1:KE1)	&
								+ DT(IS1:IE1,JS1:JE1,KS1:KE1)+DB(IS1:IE1,JS1:JE1,KS1:KE1)
	
	AE(IS1:IE1,JS1:JE1,KS1:KE1) = DE(IS1:IE1,JS1:JE1,KS1:KE1)
	AW(IS1:IE1,JS1:JE1,KS1:KE1) = DW(IS1:IE1,JS1:JE1,KS1:KE1)
	AN(IS1:IE1,JS1:JE1,KS1:KE1) = DN(IS1:IE1,JS1:JE1,KS1:KE1)
	AS(IS1:IE1,JS1:JE1,KS1:KE1) = DS(IS1:IE1,JS1:JE1,KS1:KE1)
	AT(IS1:IE1,JS1:JE1,KS1:KE1) = DT(IS1:IE1,JS1:JE1,KS1:KE1)
	AB(IS1:IE1,JS1:JE1,KS1:KE1) = DB(IS1:IE1,JS1:JE1,KS1:KE1)
	ENDIF
	
	!   TDMA solver
    
    NITER = 0 
803	NITER = NITER+1

    CALL DIF_DIAGONAL(IN,DD) ! DIAGONAL DIF OF U^* : TREATED AS EXPLICIT

	!	SUMMATION OF EXPLICIT COMPONENTS
	
	SU(IS1:IE1,JS1:JE1,KS1:KE1)	=															&
				+2.D0*RE*DJAC(IS1:IE1,JS1:JE1,KS1:KE1)/DTIME*INM(IS1:IE1,JS1:JE1,KS1:KE1)	&
				-3.D0*RE*INA(IS1:IE1,JS1:JE1,KS1:KE1) +1.D0*RE*INB(IS1:IE1,JS1:JE1,KS1:KE1)	&
				+INC(IS1:IE1,JS1:JE1,KS1:KE1) +2.D0*RE*IND(IS1:IE1,JS1:JE1,KS1:KE1)	&
				+DD(IS1:IE1,JS1:JE1,KS1:KE1)

    CALL BCU(IN,INM)

	CALL LISOLV(IS,JS,KS,IE,JE,KE,IN) ! WITHOUT OVER-RELAXATION
	
	!	U CONVERGE TEST : RESIDUAL OF U
	
	RESIU = 0.0D0
    DUM = 0.D0
	DO K=KS1,KE1
	DO J=JS1,JE1
	DO I=IS1,IE1
	    RESOR = AE(I,J,K)*IN(I+1,J,K) + AW(I,J,K)*IN(I-1,J,K) +   &
				AN(I,J,K)*IN(I,J+1,K) + AS(I,J,K)*IN(I,J-1,K) +	&
				AT(I,J,K)*IN(I,J,K+1) + AB(I,J,K)*IN(I,J,K-1) +	&
				SU(I,J,K) - AP(I,J,K)*IN(I,J,K)
		RESIU = RESIU + ABS(RESOR)
		DUM = DUM + ABS(AP(I,J,K)*IN(I,J,K) )
	ENDDO
	ENDDO
	ENDDO
	RESOR = RESIU / DUM	!	RESIU = RESIU / DUM ! SCALED RESIDUAL
	
	CALL MPI_ALLREDUCE(RESOR,RESIU,1,MPI_DOUBLE_PRECISION,MPI_MAX,MPI_COMM_WORLD,ERR)
	
	IF(RESIU.GT.RESIMAX .AND. NITER.LT.NSWPU) GOTO 803
	
    END SUBROUTINE ITER_U