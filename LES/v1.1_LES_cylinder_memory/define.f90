	SUBROUTINE DEFINE
	
	USE COMDAT_SHARED
	
	IMPLICIT NONE
	SAVE

    INCLUDE 'mpif.h'
	
	!	LAUNCHING PARALLEL PROCESS USING MPI(MASSAGE PASSING INTERFACE)
    
	CALL MPI_INIT(ERR)
	
    CALL MPI_COMM_RANK(MPI_COMM_WORLD,RANK,ERR)
    
	CALL MPI_COMM_SIZE(MPI_COMM_WORLD,NPRC,ERR)

    DIMS(0) = NDIMJ	! J-DIRECTIONAL DIMENSIONS
    
    DIMS(1) = NDIMK	! K-DIRECTIONAL DIMENSIONS
	
    REORDER(0) = .TRUE.
    
    REORDER(1) = .TRUE.
    
    PERIODS(0) = .TRUE.	! FOR BRANCH CUT ON THE J-CONSTANT PLANE
    
    PERIODS(1) = .TRUE. ! FOR PERIODIC CONDITION ON THE K-CONSTANT PLANE
	
	CALL MPI_CART_CREATE(MPI_COMM_WORLD,NDIMS,DIMS,PERIODS,REORDER,NMCW,ERR)
	
	CALL MPI_COMM_RANK(NMCW,RANK,ERR)
	
	CALL MPI_CART_COORDS(NMCW,RANK,NDIMS,COORDS,ERR)
	
	CALL MPI_CART_SHIFT(NMCW,0,1,NBR_S,NBR_N,ERR)
	
	CALL MPI_CART_SHIFT(NMCW,1,1,NBR_B,NBR_T,ERR)
	
	ALLOCATE(DISP(0:NPRC-1),STAT=ALLOC_ERR)
	
	ALLOCATE(DSIZ(0:NPRC-1),STAT=ALLOC_ERR)

	PI=4.0D0*DATAN(1.0D0)
	
	NIM1 = NI-1
	NJM1 = NJ-1
	NKM1 = NK-1
	
	NUM_GRID=(NI+1)*(NJ+1)*(NK-2)

	! DEFINE THE INTERVAL PARAMETER

	INTA = (NJ+1+(NDIMJ-1)*2)/NDIMJ
	INTB = MOD(NJ+1+(NDIMJ-1)*2,NDIMJ)

	JS = RANK/NDIMK * (INTA-2)
	JE = JS + (INTA-1)
	
	DO N=1,NDIMJ-1
		IF(NDIMJ-RANK/NDIMK.LE.INTB) JE=JE+1
		IF(NDIMJ-RANK/NDIMK.LT.INTB) JS=JS+1
		INTB=INTB-1
	ENDDO
	
	INTA = (NK+1+(NDIMK-1)*2)/NDIMK
	INTB = MOD(NK+1+(NDIMK-1)*2,NDIMK)

	KS = MOD(RANK,NDIMK) * (INTA-2)
	KE = KS + (INTA-1)
	
	DO N=1,NDIMK-1
		IF(NDIMK-MOD(RANK,NDIMK).LE.INTB) KE=KE+1
		IF(NDIMK-MOD(RANK,NDIMK).LT.INTB) KS=KS+1
		INTB=INTB-1
	ENDDO
	
	IS =0
	IS1=IS +1
	IE =NI
	IE1=IE -1
	
	JS1=JS +1
	JE1=JE -1
	KS1=KS +1
	KE1=KE -1
	
	CALL MPI_TYPE_CONTIGUOUS(IE-IS+1,MPI_DOUBLE_PRECISION,AROW,ERR)
	
	CALL MPI_TYPE_COMMIT(AROW,ERR)	! ROW ON THE I-DIRECTIONAL LINE
	
	CALL MPI_TYPE_VECTOR(JE-JS+1,1,IE-IS+1,MPI_DOUBLE_PRECISION,CLMN,ERR)
	
	CALL MPI_TYPE_COMMIT(CLMN,ERR)	! COLUMN ON THE J-DIRECTIONAL LINE
	
	CALL MPI_TYPE_CONTIGUOUS((IE-IS+1)*(JE-JS+1),MPI_DOUBLE_PRECISION,KPLN,ERR)
	
	CALL MPI_TYPE_COMMIT(KPLN,ERR)	! PLANE ON THE K-CONSTANT PLANE
	
	CALL MPI_TYPE_VECTOR(KE-KS+1,IE-IS+1,(IE-IS+1)*(JE-JS+1),MPI_DOUBLE_PRECISION,JPLN,ERR)
	
	CALL MPI_TYPE_COMMIT(JPLN,ERR)	! PLANE ON THE J-CONSTANT PLANE
	
    WRITE(*,110) RANK,NPRC,JS,JE,KS,KE
    WRITE(*,111) RANK,COORDS(0),COORDS(1)
	WRITE(*,112) RANK,NBR_N,NBR_S
	WRITE(*,113) RANK,NBR_T,NBR_B
110	FORMAT(TR1,'PROCESSOR',I3,TR1,'OF',I3,TR4,': JMIN =',I4,', JMAX =',I4,', KMIN =',I4,', KMAX =',I4)
111 FORMAT(TR1,'RANK',I3,TR1,'COORDINATES',I3,TR1,I3)
112	FORMAT(TR1,'RANK',I3,TR1,'NORTH',I3,TR1,'SOUTH',I3)
113 FORMAT(TR1,'RANK',I3,TR1,'TOP',I3,TR1,'BOTTOM',I3)
	
	END SUBROUTINE DEFINE