	SUBROUTINE STRESS_TENSOR_DY
	
	USE COMDAT_SHARED
	
	IMPLICIT NONE
	SAVE

	INCLUDE 'mpif.h'
	
	DOUBLE PRECISION :: INC

	DOUBLE PRECISION :: C_AVG
	DOUBLE PRECISION :: LM_AVG
	DOUBLE PRECISION :: MM_AVG

	DOUBLE PRECISION :: T_LM_AVG
	DOUBLE PRECISION :: T_MM_AVG
	DOUBLE PRECISION :: T_INC
	
	DOUBLE PRECISION :: CS_SQUARE,CT_SQUARE
	DOUBLE PRECISION :: MAX_CS_SQUARE,MIN_CS_SQUARE
	DOUBLE PRECISION :: MAX_CT_SQUARE,MIN_CT_SQUARE
		
	FILT_WID(IS:IE,JS:JE,KS:KE) = DJAC(IS:IE,JS:JE,KS:KE)**(1.0D0/3.0D0)
		
	!	THE LARGE-SCALE STRAIN-RATE TENSOR (STRAIN-RATE TENSOR FOR THE RESOLVED SCALE)
	LSS11(IS1:IE1,JS1:JE1,KS1:KE1)																			&
		= XIX(IS1:IE1,JS1:JE1,KS1:KE1)*(U(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - U(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
		+ ETX(IS1:IE1,JS1:JE1,KS1:KE1)*(U(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - U(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
		+ ZTX(IS1:IE1,JS1:JE1,KS1:KE1)*(U(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - U(IS1:IE1,JS1:JE1,KS1-1:KE1-1))
	LSS11(IS1:IE1,JS1:JE1,KS1:KE1) = 0.5D0*LSS11(IS1:IE1,JS1:JE1,KS1:KE1)
	
	LSS12(IS1:IE1,JS1:JE1,KS1:KE1)																			&
		= XIY(IS1:IE1,JS1:JE1,KS1:KE1)*(U(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - U(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
		+ ETY(IS1:IE1,JS1:JE1,KS1:KE1)*(U(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - U(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
		+ ZTY(IS1:IE1,JS1:JE1,KS1:KE1)*(U(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - U(IS1:IE1,JS1:JE1,KS1-1:KE1-1))	&
		+ XIX(IS1:IE1,JS1:JE1,KS1:KE1)*(V(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - V(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
		+ ETX(IS1:IE1,JS1:JE1,KS1:KE1)*(V(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - V(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
		+ ZTX(IS1:IE1,JS1:JE1,KS1:KE1)*(V(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - V(IS1:IE1,JS1:JE1,KS1-1:KE1-1))
	LSS12(IS1:IE1,JS1:JE1,KS1:KE1) = 0.25D0*LSS12(IS1:IE1,JS1:JE1,KS1:KE1)
	
	LSS13(IS1:IE1,JS1:JE1,KS1:KE1)																			&
		= XIZ(IS1:IE1,JS1:JE1,KS1:KE1)*(U(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - U(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
		+ ETZ(IS1:IE1,JS1:JE1,KS1:KE1)*(U(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - U(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
		+ ZTZ(IS1:IE1,JS1:JE1,KS1:KE1)*(U(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - U(IS1:IE1,JS1:JE1,KS1-1:KE1-1))	&
		+ XIX(IS1:IE1,JS1:JE1,KS1:KE1)*(W(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - W(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
		+ ETX(IS1:IE1,JS1:JE1,KS1:KE1)*(W(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - W(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
		+ ZTX(IS1:IE1,JS1:JE1,KS1:KE1)*(W(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - W(IS1:IE1,JS1:JE1,KS1-1:KE1-1))
	LSS13(IS1:IE1,JS1:JE1,KS1:KE1) = 0.25D0*LSS13(IS1:IE1,JS1:JE1,KS1:KE1)

	LSS21(IS1:IE1,JS1:JE1,KS1:KE1) = LSS12(IS1:IE1,JS1:JE1,KS1:KE1)

	LSS22(IS1:IE1,JS1:JE1,KS1:KE1)																			&
		= XIY(IS1:IE1,JS1:JE1,KS1:KE1)*(V(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - V(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
		+ ETY(IS1:IE1,JS1:JE1,KS1:KE1)*(V(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - V(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
		+ ZTY(IS1:IE1,JS1:JE1,KS1:KE1)*(V(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - V(IS1:IE1,JS1:JE1,KS1-1:KE1-1))
	LSS22(IS1:IE1,JS1:JE1,KS1:KE1) = 0.5D0*LSS22(IS1:IE1,JS1:JE1,KS1:KE1)

	LSS23(IS1:IE1,JS1:JE1,KS1:KE1)																			&
		= XIZ(IS1:IE1,JS1:JE1,KS1:KE1)*(V(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - V(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
		+ ETZ(IS1:IE1,JS1:JE1,KS1:KE1)*(V(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - V(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
		+ ZTZ(IS1:IE1,JS1:JE1,KS1:KE1)*(V(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - V(IS1:IE1,JS1:JE1,KS1-1:KE1-1))	&
		+ XIY(IS1:IE1,JS1:JE1,KS1:KE1)*(W(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - W(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
		+ ETY(IS1:IE1,JS1:JE1,KS1:KE1)*(W(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - W(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
		+ ZTY(IS1:IE1,JS1:JE1,KS1:KE1)*(W(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - W(IS1:IE1,JS1:JE1,KS1-1:KE1-1))
	LSS23(IS1:IE1,JS1:JE1,KS1:KE1) = 0.25D0*LSS23(IS1:IE1,JS1:JE1,KS1:KE1)
	
	LSS31(IS1:IE1,JS1:JE1,KS1:KE1) = LSS13(IS1:IE1,JS1:JE1,KS1:KE1)

	LSS32(IS1:IE1,JS1:JE1,KS1:KE1) = LSS23(IS1:IE1,JS1:JE1,KS1:KE1)

	LSS33(IS1:IE1,JS1:JE1,KS1:KE1)																			&
		= XIZ(IS1:IE1,JS1:JE1,KS1:KE1)*(W(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - W(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
		+ ETZ(IS1:IE1,JS1:JE1,KS1:KE1)*(W(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - W(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
		+ ZTZ(IS1:IE1,JS1:JE1,KS1:KE1)*(W(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - W(IS1:IE1,JS1:JE1,KS1-1:KE1-1))
	LSS33(IS1:IE1,JS1:JE1,KS1:KE1) = 0.5D0*LSS33(IS1:IE1,JS1:JE1,KS1:KE1)
	
	DTDX(IS1:IE1,JS1:JE1,KS1:KE1)																			&
		= XIX(IS1:IE1,JS1:JE1,KS1:KE1)*(T(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - T(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
		+ ETX(IS1:IE1,JS1:JE1,KS1:KE1)*(T(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - T(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
		+ ZTX(IS1:IE1,JS1:JE1,KS1:KE1)*(T(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - T(IS1:IE1,JS1:JE1,KS1-1:KE1-1))
	DTDX(IS1:IE1,JS1:JE1,KS1:KE1) = 0.5D0*DTDX(IS1:IE1,JS1:JE1,KS1:KE1)
	
	DTDY(IS1:IE1,JS1:JE1,KS1:KE1)																			&
		= XIY(IS1:IE1,JS1:JE1,KS1:KE1)*(T(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - T(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
		+ ETY(IS1:IE1,JS1:JE1,KS1:KE1)*(T(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - T(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
		+ ZTY(IS1:IE1,JS1:JE1,KS1:KE1)*(T(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - T(IS1:IE1,JS1:JE1,KS1-1:KE1-1))
	DTDY(IS1:IE1,JS1:JE1,KS1:KE1) = 0.5D0*DTDY(IS1:IE1,JS1:JE1,KS1:KE1)

	DTDZ(IS1:IE1,JS1:JE1,KS1:KE1)																			&
		= XIZ(IS1:IE1,JS1:JE1,KS1:KE1)*(T(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - T(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
		+ ETZ(IS1:IE1,JS1:JE1,KS1:KE1)*(T(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - T(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
		+ ZTZ(IS1:IE1,JS1:JE1,KS1:KE1)*(T(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - T(IS1:IE1,JS1:JE1,KS1-1:KE1-1))
	DTDZ(IS1:IE1,JS1:JE1,KS1:KE1) = 0.5D0*DTDZ(IS1:IE1,JS1:JE1,KS1:KE1)
	
	DO K=KS1,KE1
	DO J=JS1,JE1
	I=0
		LSS11(I,J,K) = 0.5D0*(2.0D0*XIX(I,J,K)*(U(I+1,J,K) - U(I,J,K))		&
						           +ETX(I,J,K)*(U(I,J+1,K) - U(I,J-1,K))	&
							       +ZTX(I,J,K)*(U(I,J,K+1) - U(I,J,K-1)) )
	
		LSS12(I,J,K) = 0.25D0*(2.0D0*XIY(I,J,K)*(U(I+1,J,K) - U(I,J,K))		&
									+ETY(I,J,K)*(U(I,J+1,K) - U(I,J-1,K))	&
									+ZTY(I,J,K)*(U(I,J,K+1) - U(I,J,K-1))	& 
							  +2.0D0*XIX(I,J,K)*(V(I+1,J,K) - V(I,J,K))		&
									+ETX(I,J,K)*(V(I,J+1,K) - V(I,J-1,K))	&
									+ZTX(I,J,K)*(V(I,J,K+1) - V(I,J,K-1)) )

		LSS13(I,J,K) = 0.25D0*(2.0D0*XIZ(I,J,K)*(U(I+1,J,K) - U(I,J,K))		&
									+ETZ(I,J,K)*(U(I,J+1,K) - U(I,J-1,K))	&
									+ZTZ(I,J,K)*(U(I,J,K+1) - U(I,J,K-1))	&
							  +2.0D0*XIX(I,J,K)*(W(I+1,J,K) - W(I,J,K))		&
									+ETX(I,J,K)*(W(I,J+1,K) - W(I,J-1,K))	&
									+ZTX(I,J,K)*(W(I,J,K+1) - W(I,J,K-1)) )

		LSS21(I,J,K) = LSS12(I,J,K)

		LSS22(I,J,K) = 0.5D0*(2.0D0*XIY(I,J,K)*(V(I+1,J,K) - V(I,J,K))		&
								   +ETY(I,J,K)*(V(I,J+1,K) - V(I,J-1,K))	&
							       +ZTY(I,J,K)*(V(I,J,K+1) - V(I,J,K-1)) )

		LSS23(I,J,K) = 0.25D0*(2.0D0*XIZ(I,J,K)*(V(I+1,J,K) - V(I,J,K))		&
									+ETZ(I,J,K)*(V(I,J+1,K) - V(I,J-1,K))	&
									+ZTZ(I,J,K)*(V(I,J,K+1) - V(I,J,K-1))	&
							  +2.0D0*XIY(I,J,K)*(W(I+1,J,K) - W(I,J,K))		&
									+ETY(I,J,K)*(W(I,J+1,K) - W(I,J-1,K))	&
									+ZTY(I,J,K)*(W(I,J,K+1) - W(I,J,K-1)) )

		LSS31(I,J,K) = LSS13(I,J,K)

		LSS32(I,J,K) = LSS23(I,J,K)

		LSS33(I,J,K) = 0.5D0*(2.0D0*XIZ(I,J,K)*(W(I+1,J,K) - W(I,J,K))		&
								   +ETZ(I,J,K)*(W(I,J+1,K) - W(I,J-1,K))	&
								   +ZTZ(I,J,K)*(W(I,J,K+1) - W(I,J,K-1)) )
		
		DTDX(I,J,K) = 0.5D0*(2.0D0*XIX(I,J,K)*(T(I+1,J,K) - T(I,J,K))	&
						          +ETX(I,J,K)*(T(I,J+1,K) - T(I,J-1,K))	&
							      +ZTX(I,J,K)*(T(I,J,K+1) - T(I,J,K-1)) )
		DTDY(I,J,K) = 0.5D0*(2.0D0*XIY(I,J,K)*(T(I+1,J,K) - T(I,J,K))	&
						          +ETY(I,J,K)*(T(I,J+1,K) - T(I,J-1,K))	&
							      +ZTY(I,J,K)*(T(I,J,K+1) - T(I,J,K-1)) )
		DTDZ(I,J,K) = 0.5D0*(2.0D0*XIZ(I,J,K)*(T(I+1,J,K) - T(I,J,K))	&
						          +ETZ(I,J,K)*(T(I,J+1,K) - T(I,J-1,K))	&
							      +ZTZ(I,J,K)*(T(I,J,K+1) - T(I,J,K-1)) )
								   
	I=NI
		LSS11(I,J,K) = 0.5D0*(2.0D0*XIX(I,J,K)*(U(I,J,K)   - U(I-1,J,K))	&
						           +ETX(I,J,K)*(U(I,J+1,K) - U(I,J-1,K))	&
							       +ZTX(I,J,K)*(U(I,J,K+1) - U(I,J,K-1)) )
	
		LSS12(I,J,K) = 0.25D0*(2.0D0*XIY(I,J,K)*(U(I,J,K)   - U(I-1,J,K))	&
									+ETY(I,J,K)*(U(I,J+1,K) - U(I,J-1,K))	&
									+ZTY(I,J,K)*(U(I,J,K+1) - U(I,J,K-1))	&
							  +2.0D0*XIX(I,J,K)*(V(I,J,K)   - V(I-1,J,K))	&
									+ETX(I,J,K)*(V(I,J+1,K) - V(I,J-1,K))	&
									+ZTX(I,J,K)*(V(I,J,K+1) - V(I,J,K-1)) )

		LSS13(I,J,K) = 0.25D0*(2.0D0*XIZ(I,J,K)*(U(I,J,K)   - U(I-1,J,K))	&
									+ETZ(I,J,K)*(U(I,J+1,K) - U(I,J-1,K))	&
							        +ZTZ(I,J,K)*(U(I,J,K+1) - U(I,J,K-1))	&
							  +2.0D0*XIX(I,J,K)*(W(I,J,K)   - W(I-1,J,K))	&
									+ETX(I,J,K)*(W(I,J+1,K) - W(I,J-1,K))	&
									+ZTX(I,J,K)*(W(I,J,K+1) - W(I,J,K-1)) )

		LSS21(I,J,K) = LSS12(I,J,K)

		LSS22(I,J,K) = 0.5D0*(2.0D0*XIY(I,J,K)*(V(I,J,K)   - V(I-1,J,K))	&
								   +ETY(I,J,K)*(V(I,J+1,K) - V(I,J-1,K))	&
							       +ZTY(I,J,K)*(V(I,J,K+1) - V(I,J,K-1)) )

		LSS23(I,J,K) = 0.25D0*(2.0D0*XIZ(I,J,K)*(V(I,J,K)   - V(I-1,J,K))	&
									+ETZ(I,J,K)*(V(I,J+1,K) - V(I,J-1,K))	&
									+ZTZ(I,J,K)*(V(I,J,K+1) - V(I,J,K-1))	&
							  +2.0D0*XIY(I,J,K)*(W(I,J,K)   - W(I-1,J,K))	&
									+ETY(I,J,K)*(W(I,J+1,K) - W(I,J-1,K))	&
									+ZTY(I,J,K)*(W(I,J,K+1) - W(I,J,K-1)) )

		LSS31(I,J,K) = LSS13(I,J,K)

		LSS32(I,J,K) = LSS23(I,J,K)

		LSS33(I,J,K) = 0.5D0*(2.0D0*XIZ(I,J,K)*(W(I,J,K)   - W(I-1,J,K)) &
								   +ETZ(I,J,K)*(W(I,J+1,K) - W(I,J-1,K)) &
								   +ZTZ(I,J,K)*(W(I,J,K+1) - W(I,J,K-1)) )
								   
		DTDX(I,J,K) = 0.5D0*(2.0D0*XIX(I,J,K)*(T(I,J,K) - T(I-1,J,K))	&
						          +ETX(I,J,K)*(T(I,J+1,K) - T(I,J-1,K))	&
							      +ZTX(I,J,K)*(T(I,J,K+1) - T(I,J,K-1)) )
		DTDY(I,J,K) = 0.5D0*(2.0D0*XIY(I,J,K)*(T(I,J,K) - T(I-1,J,K))	&
						          +ETY(I,J,K)*(T(I,J+1,K) - T(I,J-1,K))	&
							      +ZTY(I,J,K)*(T(I,J,K+1) - T(I,J,K-1)) )
		DTDZ(I,J,K) = 0.5D0*(2.0D0*XIZ(I,J,K)*(T(I,J,K) - T(I-1,J,K))	&
						          +ETZ(I,J,K)*(T(I,J+1,K) - T(I,J-1,K))	&
							      +ZTZ(I,J,K)*(T(I,J,K+1) - T(I,J,K-1)) )
	ENDDO
	ENDDO

	CALL MPI_X(LSS11)
	CALL MPI_X(LSS12)
	CALL MPI_X(LSS13)
	
	CALL MPI_X(LSS21)
	CALL MPI_X(LSS22)
	CALL MPI_X(LSS23)
	
	CALL MPI_X(LSS31)
	CALL MPI_X(LSS32)
	CALL MPI_X(LSS33)
	
	CALL MPI_X(DTDX)
	CALL MPI_X(DTDY)
	CALL MPI_X(DTDZ)

	!   THE MAGNITUDE OF LARGE-SCALE STRAIN-RATE TENSOR

	MAGNITUDE_LSS(IS:IE,JS:JE,KS:KE) = DSQRT(2.D0*(													&
		+ LSS11(IS:IE,JS:JE,KS:KE)**2 + LSS12(IS:IE,JS:JE,KS:KE)**2	+ LSS13(IS:IE,JS:JE,KS:KE)**2	&
		+ LSS21(IS:IE,JS:JE,KS:KE)**2 + LSS22(IS:IE,JS:JE,KS:KE)**2	+ LSS23(IS:IE,JS:JE,KS:KE)**2	&
		+ LSS31(IS:IE,JS:JE,KS:KE)**2 + LSS32(IS:IE,JS:JE,KS:KE)**2	+ LSS33(IS:IE,JS:JE,KS:KE)**2 ) )
		
	CALL TEST_FILTER(U,U_HAT)
	CALL TEST_FILTER(V,V_HAT)
	CALL TEST_FILTER(W,W_HAT)
	CALL TEST_FILTER(T,T_HAT)
    
    UIN = 1.D0
    CALL BCU(U_HAT,U_N)
	UIN = 0.D0
    CALL BCU(V_HAT,V_N)
    CALL BCU(W_HAT,W_N)
    CALL BCT(T_HAT,T_N)
	
	!	TEST-FILTERED RESOLVED STRAIN RATE TENSOR
	DO K=KS1,KE1
	DO J=JS1,JE1
	DO I=IS1,IE1
	LSS11_HAT											&
		= XIX(I,J,K)*(U_HAT(I+1,J,K) - U_HAT(I-1,J,K))	&
		+ ETX(I,J,K)*(U_HAT(I,J+1,K) - U_HAT(I,J-1,K))	&
		+ ZTX(I,J,K)*(U_HAT(I,J,K+1) - U_HAT(I,J,K-1))
	LSS11_HAT = 0.5D0*LSS11_HAT
	
	LSS12_HAT											&
		= XIY(I,J,K)*(U_HAT(I+1,J,K) - U_HAT(I-1,J,K))	&
		+ ETY(I,J,K)*(U_HAT(I,J+1,K) - U_HAT(I,J-1,K))	&
		+ ZTY(I,J,K)*(U_HAT(I,J,K+1) - U_HAT(I,J,K-1))	&
		+ XIX(I,J,K)*(V_HAT(I+1,J,K) - V_HAT(I-1,J,K))	&
		+ ETX(I,J,K)*(V_HAT(I,J+1,K) - V_HAT(I,J-1,K))	&
		+ ZTX(I,J,K)*(V_HAT(I,J,K+1) - V_HAT(I,J,K-1))
	LSS12_HAT = 0.25D0*LSS12_HAT
	
	LSS13_HAT											&
		= XIZ(I,J,K)*(U_HAT(I+1,J,K) - U_HAT(I-1,J,K))	&
		+ ETZ(I,J,K)*(U_HAT(I,J+1,K) - U_HAT(I,J-1,K))	&
		+ ZTZ(I,J,K)*(U_HAT(I,J,K+1) - U_HAT(I,J,K-1))	&
		+ XIX(I,J,K)*(W_HAT(I+1,J,K) - W_HAT(I-1,J,K))	&
		+ ETX(I,J,K)*(W_HAT(I,J+1,K) - W_HAT(I,J-1,K))	&
		+ ZTX(I,J,K)*(W_HAT(I,J,K+1) - W_HAT(I,J,K-1))
	LSS13_HAT = 0.25D0*LSS13_HAT

	LSS21_HAT = LSS12_HAT

	LSS22_HAT											&
		= XIY(I,J,K)*(V_HAT(I+1,J,K) - V_HAT(I-1,J,K))	&
		+ ETY(I,J,K)*(V_HAT(I,J+1,K) - V_HAT(I,J-1,K))	&
		+ ZTY(I,J,K)*(V_HAT(I,J,K+1) - V_HAT(I,J,K-1))
	LSS22_HAT = 0.5D0*LSS22_HAT

	LSS23_HAT											&
		= XIZ(I,J,K)*(V_HAT(I+1,J,K) - V_HAT(I-1,J,K))	&
		+ ETZ(I,J,K)*(V_HAT(I,J+1,K) - V_HAT(I,J-1,K))	&
		+ ZTZ(I,J,K)*(V_HAT(I,J,K+1) - V_HAT(I,J,K-1))	&
		+ XIY(I,J,K)*(W_HAT(I+1,J,K) - W_HAT(I-1,J,K))	&
		+ ETY(I,J,K)*(W_HAT(I,J+1,K) - W_HAT(I,J-1,K))	&
		+ ZTY(I,J,K)*(W_HAT(I,J,K+1) - W_HAT(I,J,K-1))
	LSS23_HAT = 0.25D0*LSS23_HAT
	
	LSS31_HAT = LSS13_HAT

	LSS32_HAT = LSS23_HAT

	LSS33_HAT											&
		= XIZ(I,J,K)*(W_HAT(I+1,J,K) - W_HAT(I-1,J,K))	&
		+ ETZ(I,J,K)*(W_HAT(I,J+1,K) - W_HAT(I,J-1,K))	&
		+ ZTZ(I,J,K)*(W_HAT(I,J,K+1) - W_HAT(I,J,K-1))
	LSS33_HAT = 0.5D0*LSS33_HAT
	
	DUM = 2.D0*(+ LSS11_HAT **2 + LSS12_HAT **2	+ LSS13_HAT **2	&
				+ LSS21_HAT **2 + LSS22_HAT **2	+ LSS23_HAT **2	&
				+ LSS31_HAT **2 + LSS32_HAT **2	+ LSS33_HAT **2	)
		
	MAGNITUDE_LSS_HAT(I,J,K) = DSQRT(DUM)
	
	ENDDO
	ENDDO
	ENDDO
	
!-------------------------------------------------------------------------------------------------------------------------
	!	COMPUTE L11
	DUM_IN(IS:IE,JS:JE,KS:KE) =	U(IS:IE,JS:JE,KS:KE) * U(IS:IE,JS:JE,KS:KE)
	
	CALL TEST_FILTER(DUM_IN,DUM_OUT)

	L_IJ(IS1:IE1,JS1:JE1,KS1:KE1) = - DUM_OUT(IS1:IE1,JS1:JE1,KS1:KE1) + U_HAT(IS1:IE1,JS1:JE1,KS1:KE1)**2

	!	COMPUTE M11
	DUM_IN(IS:IE,JS:JE,KS:KE) =	MAGNITUDE_LSS(IS:IE,JS:JE,KS:KE) * LSS11(IS:IE,JS:JE,KS:KE)

	CALL TEST_FILTER(DUM_IN,DUM_OUT)
	
	M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =								&
		+ 4.0D0*MAGNITUDE_LSS_HAT(IS1:IE1,JS1:JE1,KS1:KE1)		&
		* 0.5D0*(XIX(IS1:IE1,JS1:JE1,KS1:KE1)*(U_HAT(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - U_HAT(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
				+ETX(IS1:IE1,JS1:JE1,KS1:KE1)*(U_HAT(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - U_HAT(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
				+ZTX(IS1:IE1,JS1:JE1,KS1:KE1)*(U_HAT(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - U_HAT(IS1:IE1,JS1:JE1,KS1-1:KE1-1))	)

	M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =	FILT_WID(IS1:IE1,JS1:JE1,KS1:KE1) * FILT_WID(IS1:IE1,JS1:JE1,KS1:KE1)*	&
									(M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) - DUM_OUT(IS1:IE1,JS1:JE1,KS1:KE1))

	!	COMPUTE L11*M11 
	!	AND		M11*M11
	LM_IJ(IS1:IE1,JS1:JE1,KS1:KE1) = L_IJ(IS1:IE1,JS1:JE1,KS1:KE1) * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)

	MM_IJ(IS1:IE1,JS1:JE1,KS1:KE1) = M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)

	!	COMPUTE L22
	DUM_IN(IS:IE,JS:JE,KS:KE) =	V(IS:IE,JS:JE,KS:KE) * V(IS:IE,JS:JE,KS:KE)

	CALL TEST_FILTER(DUM_IN,DUM_OUT)
	
	L_IJ(IS1:IE1,JS1:JE1,KS1:KE1) = - DUM_OUT(IS1:IE1,JS1:JE1,KS1:KE1) + V_HAT(IS1:IE1,JS1:JE1,KS1:KE1)**2

	!	COMPUTE M22
	DUM_IN(IS:IE,JS:JE,KS:KE) =	MAGNITUDE_LSS(IS:IE,JS:JE,KS:KE) * LSS22(IS:IE,JS:JE,KS:KE)

	CALL TEST_FILTER(DUM_IN,DUM_OUT)
	
	M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =																						&
		+ 4.0D0*MAGNITUDE_LSS_HAT(IS1:IE1,JS1:JE1,KS1:KE1)																&
		* 0.5D0*(XIY(IS1:IE1,JS1:JE1,KS1:KE1)*(V_HAT(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - V_HAT(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
				+ETY(IS1:IE1,JS1:JE1,KS1:KE1)*(V_HAT(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - V_HAT(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
				+ZTY(IS1:IE1,JS1:JE1,KS1:KE1)*(V_HAT(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - V_HAT(IS1:IE1,JS1:JE1,KS1-1:KE1-1)))
	
	M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =	FILT_WID(IS1:IE1,JS1:JE1,KS1:KE1) * FILT_WID(IS1:IE1,JS1:JE1,KS1:KE1)*	&
									(M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) - DUM_OUT(IS1:IE1,JS1:JE1,KS1:KE1))

	!	COMPUTE	L11*M11 + L22*M22 
	!	AND		M11*M11 + M22*M22
	LM_IJ(IS1:IE1,JS1:JE1,KS1:KE1)= LM_IJ(IS1:IE1,JS1:JE1,KS1:KE1) +	&
									L_IJ(IS1:IE1,JS1:JE1,KS1:KE1) * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)

	MM_IJ(IS1:IE1,JS1:JE1,KS1:KE1)=	MM_IJ(IS1:IE1,JS1:JE1,KS1:KE1) +	&
									M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)

	!	COMPUTE L33
	DUM_IN(IS:IE,JS:JE,KS:KE) =	W(IS:IE,JS:JE,KS:KE) * W(IS:IE,JS:JE,KS:KE)

	CALL TEST_FILTER(DUM_IN,DUM_OUT)
	
	L_IJ(IS1:IE1,JS1:JE1,KS1:KE1) = - DUM_OUT(IS1:IE1,JS1:JE1,KS1:KE1) + W_HAT(IS1:IE1,JS1:JE1,KS1:KE1)**2
			
	!	COMPUTE M33
	DUM_IN(IS:IE,JS:JE,KS:KE) =	MAGNITUDE_LSS(IS:IE,JS:JE,KS:KE) * LSS33(IS:IE,JS:JE,KS:KE)

	CALL TEST_FILTER(DUM_IN,DUM_OUT)
	
	M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)   =																					&
		+ 4.0D0*MAGNITUDE_LSS_HAT(IS1:IE1,JS1:JE1,KS1:KE1)																&
		* 0.5D0*(XIZ(IS1:IE1,JS1:JE1,KS1:KE1)*(W_HAT(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - W_HAT(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
				+ETZ(IS1:IE1,JS1:JE1,KS1:KE1)*(W_HAT(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - W_HAT(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
				+ZTZ(IS1:IE1,JS1:JE1,KS1:KE1)*(W_HAT(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - W_HAT(IS1:IE1,JS1:JE1,KS1-1:KE1-1))	)

	M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =	FILT_WID(IS1:IE1,JS1:JE1,KS1:KE1) * FILT_WID(IS1:IE1,JS1:JE1,KS1:KE1)*	&
									(M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) - DUM_OUT(IS1:IE1,JS1:JE1,KS1:KE1))

	!	COMPUTE	L11*M11 + L22*M22 + L33*M33 
	!	AND		M11*M11 + M22*M22 + M33*M33
	LM_IJ(IS1:IE1,JS1:JE1,KS1:KE1)=	LM_IJ(IS1:IE1,JS1:JE1,KS1:KE1) +	&
									L_IJ(IS1:IE1,JS1:JE1,KS1:KE1) * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)
		
	MM_IJ(IS1:IE1,JS1:JE1,KS1:KE1)=	MM_IJ(IS1:IE1,JS1:JE1,KS1:KE1) +	&
									M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)

	!	COMPUTE L12
	DUM_IN(IS:IE,JS:JE,KS:KE) =	U(IS:IE,JS:JE,KS:KE) * V(IS:IE,JS:JE,KS:KE)
	
	CALL TEST_FILTER(DUM_IN,DUM_OUT)

	L_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =	- DUM_OUT(IS1:IE1,JS1:JE1,KS1:KE1)								&
									+ U_HAT(IS1:IE1,JS1:JE1,KS1:KE1)*V_HAT(IS1:IE1,JS1:JE1,KS1:KE1)

	!	COMPUTE M12
	DUM_IN(IS:IE,JS:JE,KS:KE) =	MAGNITUDE_LSS(IS:IE,JS:JE,KS:KE) * LSS12(IS:IE,JS:JE,KS:KE)

	CALL TEST_FILTER(DUM_IN,DUM_OUT)
	
	M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)   =							&
		+4.0D0*MAGNITUDE_LSS_HAT(IS1:IE1,JS1:JE1,KS1:KE1)		&
		*0.25D0*(XIY(IS1:IE1,JS1:JE1,KS1:KE1)*(U_HAT(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - U_HAT(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
				+ETY(IS1:IE1,JS1:JE1,KS1:KE1)*(U_HAT(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - U_HAT(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
				+ZTY(IS1:IE1,JS1:JE1,KS1:KE1)*(U_HAT(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - U_HAT(IS1:IE1,JS1:JE1,KS1-1:KE1-1))	&
				+XIX(IS1:IE1,JS1:JE1,KS1:KE1)*(V_HAT(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - V_HAT(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
				+ETX(IS1:IE1,JS1:JE1,KS1:KE1)*(V_HAT(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - V_HAT(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
				+ZTX(IS1:IE1,JS1:JE1,KS1:KE1)*(V_HAT(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - V_HAT(IS1:IE1,JS1:JE1,KS1-1:KE1-1))	)

	M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =	FILT_WID(IS1:IE1,JS1:JE1,KS1:KE1) * FILT_WID(IS1:IE1,JS1:JE1,KS1:KE1)*	&
									(M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) - DUM_OUT(IS1:IE1,JS1:JE1,KS1:KE1))

	!	COMPUTE	L11*M11 + L22*M22 + L33*M33 + 2*L12*M12
	!   AND		M11*M11 + M22*M22 + M33*M33 + 2*M12*M12
	LM_IJ(IS1:IE1,JS1:JE1,KS1:KE1)=	LM_IJ(IS1:IE1,JS1:JE1,KS1:KE1) +	&
							2.0D0 * L_IJ(IS1:IE1,JS1:JE1,KS1:KE1) * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)
	
	MM_IJ(IS1:IE1,JS1:JE1,KS1:KE1)=	MM_IJ(IS1:IE1,JS1:JE1,KS1:KE1) +	&
							2.0D0 * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)

	!	COMPUTE L13
	DUM_IN(IS:IE,JS:JE,KS:KE) =	U(IS:IE,JS:JE,KS:KE) * W(IS:IE,JS:JE,KS:KE)
	
	CALL TEST_FILTER(DUM_IN,DUM_OUT)

	L_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =	- DUM_OUT(IS1:IE1,JS1:JE1,KS1:KE1)								&
									+ U_HAT(IS1:IE1,JS1:JE1,KS1:KE1)*W_HAT(IS1:IE1,JS1:JE1,KS1:KE1)

	!	COMPUTE M13
	DUM_IN(IS:IE,JS:JE,KS:KE) =	MAGNITUDE_LSS(IS:IE,JS:JE,KS:KE) * LSS13(IS:IE,JS:JE,KS:KE)

	CALL TEST_FILTER(DUM_IN,DUM_OUT)
	
	M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =								&
		+4.0D0*MAGNITUDE_LSS_HAT(IS1:IE1,JS1:JE1,KS1:KE1)		&
		*0.25D0*(XIZ(IS1:IE1,JS1:JE1,KS1:KE1)*(U_HAT(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - U_HAT(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
				+ETZ(IS1:IE1,JS1:JE1,KS1:KE1)*(U_HAT(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - U_HAT(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
				+ZTZ(IS1:IE1,JS1:JE1,KS1:KE1)*(U_HAT(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - U_HAT(IS1:IE1,JS1:JE1,KS1-1:KE1-1))	&
				+XIX(IS1:IE1,JS1:JE1,KS1:KE1)*(W_HAT(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - W_HAT(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
				+ETX(IS1:IE1,JS1:JE1,KS1:KE1)*(W_HAT(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - W_HAT(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
				+ZTX(IS1:IE1,JS1:JE1,KS1:KE1)*(W_HAT(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - W_HAT(IS1:IE1,JS1:JE1,KS1-1:KE1-1))	)
	
	M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =	FILT_WID(IS1:IE1,JS1:JE1,KS1:KE1) * FILT_WID(IS1:IE1,JS1:JE1,KS1:KE1)* &
									(M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) - DUM_OUT(IS1:IE1,JS1:JE1,KS1:KE1))

	!	COMPUTE	L11*M11 + L22*M22 + L33*M33 + 2*L12*M12 + 2*L13*M13
	!	AND		M11*M11 + M22*M22 + M33*M33 + 2*M12*M12 + 2*M13*M13
	LM_IJ(IS1:IE1,JS1:JE1,KS1:KE1)=	LM_IJ(IS1:IE1,JS1:JE1,KS1:KE1) +	&
							2.0D0 * L_IJ(IS1:IE1,JS1:JE1,KS1:KE1) * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)

	MM_IJ(IS1:IE1,JS1:JE1,KS1:KE1)=	MM_IJ(IS1:IE1,JS1:JE1,KS1:KE1) +	&
							2.0D0 * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)

	!	COMPUTE L23
	DUM_IN(IS:IE,JS:JE,KS:KE) =	V(IS:IE,JS:JE,KS:KE) * W(IS:IE,JS:JE,KS:KE)

	CALL TEST_FILTER(DUM_IN,DUM_OUT)

	L_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =	- DUM_OUT(IS1:IE1,JS1:JE1,KS1:KE1)								&
									+ V_HAT(IS1:IE1,JS1:JE1,KS1:KE1)*W_HAT(IS1:IE1,JS1:JE1,KS1:KE1)

	!	COMPUTE M23
	DUM_IN(IS:IE,JS:JE,KS:KE) =	MAGNITUDE_LSS(IS:IE,JS:JE,KS:KE) * LSS23(IS:IE,JS:JE,KS:KE)

	CALL TEST_FILTER(DUM_IN,DUM_OUT)
	
	M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)   =							&
		+4.0D0*MAGNITUDE_LSS_HAT(IS1:IE1,JS1:JE1,KS1:KE1)		&
		*0.25D0*(XIZ(IS1:IE1,JS1:JE1,KS1:KE1)*(V_HAT(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - V_HAT(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
				+ETZ(IS1:IE1,JS1:JE1,KS1:KE1)*(V_HAT(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - V_HAT(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
				+ZTZ(IS1:IE1,JS1:JE1,KS1:KE1)*(V_HAT(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - V_HAT(IS1:IE1,JS1:JE1,KS1-1:KE1-1))	&
				+XIY(IS1:IE1,JS1:JE1,KS1:KE1)*(W_HAT(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - W_HAT(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
				+ETY(IS1:IE1,JS1:JE1,KS1:KE1)*(W_HAT(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - W_HAT(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
				+ZTY(IS1:IE1,JS1:JE1,KS1:KE1)*(W_HAT(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - W_HAT(IS1:IE1,JS1:JE1,KS1-1:KE1-1))	)

	M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =	FILT_WID(IS1:IE1,JS1:JE1,KS1:KE1) * FILT_WID(IS1:IE1,JS1:JE1,KS1:KE1)*	&
									(M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) - DUM_OUT(IS1:IE1,JS1:JE1,KS1:KE1))

	!	COMPUTE	L11*M11 + L22*M22 + L33*M33 + 2*L12*M12 + 2*L13*M13 + 2*L23*M23
	!	AND		M11*M11 + M22*M22 + M33*M33 + 2*M12*M12 + 2*M13*M13 + 2*M23*M23
	LM_IJ(IS1:IE1,JS1:JE1,KS1:KE1)=	LM_IJ(IS1:IE1,JS1:JE1,KS1:KE1) +	&
							2.0D0 * L_IJ(IS1:IE1,JS1:JE1,KS1:KE1) * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)

	MM_IJ(IS1:IE1,JS1:JE1,KS1:KE1)=	MM_IJ(IS1:IE1,JS1:JE1,KS1:KE1) +	&
							2.0D0 * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)
	
!	DYNAMIC (TIME-DEPENDENT,VOLUME-AVERAGED) EDDY VISCOSITY
	C_AVG  = 0.0D0
	LM_AVG = 0.0D0
	MM_AVG = 0.0D0
	INC    = 0.0D0
	MAX_CS_SQUARE = 0.0D0
	MIN_CS_SQUARE = 0.0D0
	
	DO K = KS1,KE1
	DO J = JS1,JE1
	DO I = IS1,IE1
		CS_SQUARE = 0.5D0 * LM_IJ(I,J,K)/MM_IJ(I,J,K)
		MAX_CS_SQUARE = MAX(MAX_CS_SQUARE,CS_SQUARE)
		MIN_CS_SQUARE = MIN(MIN_CS_SQUARE,CS_SQUARE)
		IF(CS_SQUARE.GE.0.D0) THEN
			LM_AVG = LM_AVG + LM_IJ(I,J,K)
			MM_AVG = MM_AVG + MM_IJ(I,J,K)
			INC = INC + 1.D0
		ENDIF
	ENDDO
	ENDDO
	ENDDO
	
	CALL MPI_ALLREDUCE(LM_AVG,T_LM_AVG,1,MPI_DOUBLE_PRECISION, MPI_SUM,NMCW,ERR)
	CALL MPI_ALLREDUCE(MM_AVG,T_MM_AVG,1,MPI_DOUBLE_PRECISION, MPI_SUM,NMCW,ERR)
	CALL MPI_ALLREDUCE(INC,T_INC,1,MPI_DOUBLE_PRECISION, MPI_SUM,NMCW,ERR)

	T_LM_AVG = T_LM_AVG / T_INC
	T_MM_AVG = T_MM_AVG / T_INC
	
	C_AVG = 0.5D0 * T_LM_AVG/T_MM_AVG
	
	IF(T_MM_AVG.LT.1D-6)	C_AVG = 0.D0
	
	IF(RANK.EQ.0) THEN
		OPEN(UNIT=12,FILE='CS.PLT',STATUS='OLD',ACCESS='APPEND')
		WRITE(12,*) RTIME,DSQRT(C_AVG)
		CLOSE(12)
	ENDIF
	
	!	THE EDDY VISCOSITY
	EDDY_VISCOS(IS:IE,JS:JE,KS:KE) = C_AVG*FILT_WID(IS:IE,JS:JE,KS:KE)**2*MAGNITUDE_LSS(IS:IE,JS:JE,KS:KE)
	
!	SUBGRID-SCALE STRESS TENSOR
!	SGS11 =	-2.D0*EDDY_VISCOS(I,J,K)*LSS11(I,J,K)
!	SGS12 =	-2.D0*EDDY_VISCOS(I,J,K)*LSS12(I,J,K)
!	SGS13 =	-2.D0*EDDY_VISCOS(I,J,K)*LSS13(I,J,K)
!	SGS21 =	-2.D0*EDDY_VISCOS(I,J,K)*LSS21(I,J,K)
!	SGS22 =	-2.D0*EDDY_VISCOS(I,J,K)*LSS22(I,J,K)
!	SGS23 =	-2.D0*EDDY_VISCOS(I,J,K)*LSS23(I,J,K)
!	SGS31 =	-2.D0*EDDY_VISCOS(I,J,K)*LSS31(I,J,K)
!	SGS32 =	-2.D0*EDDY_VISCOS(I,J,K)*LSS32(I,J,K)
!	SGS33 =	-2.D0*EDDY_VISCOS(I,J,K)*LSS33(I,J,K)
	
	!	DIVERSENCE OF SUBGRID-SCALE STRESS TENSOR
	DO K=KS1,KE1
	DO J=JS1,JE1
	DO I=IS1,IE1
	
	SGS_X(I,J,K) = 0.5D0*(	&
		+ XIX(I,J,K)*(-2.D0*EDDY_VISCOS(I+1,J,K)*LSS11(I+1,J,K) +2.D0*EDDY_VISCOS(I-1,J,K)*LSS11(I-1,J,K))	&
		+ ETX(I,J,K)*(-2.D0*EDDY_VISCOS(I,J+1,K)*LSS11(I,J+1,K) +2.D0*EDDY_VISCOS(I,J-1,K)*LSS11(I,J-1,K))	&
		+ ZTX(I,J,K)*(-2.D0*EDDY_VISCOS(I,J,K+1)*LSS11(I,J,K+1) +2.D0*EDDY_VISCOS(I,J,K-1)*LSS11(I,J,K-1))	&
	
		+ XIY(I,J,K)*(-2.D0*EDDY_VISCOS(I+1,J,K)*LSS12(I+1,J,K) +2.D0*EDDY_VISCOS(I-1,J,K)*LSS12(I-1,J,K))	&
		+ ETY(I,J,K)*(-2.D0*EDDY_VISCOS(I,J+1,K)*LSS12(I,J+1,K) +2.D0*EDDY_VISCOS(I,J-1,K)*LSS12(I,J-1,K))	&
		+ ZTY(I,J,K)*(-2.D0*EDDY_VISCOS(I,J,K+1)*LSS12(I,J,K+1) +2.D0*EDDY_VISCOS(I,J,K-1)*LSS12(I,J,K-1))	&
						
		+ XIZ(I,J,K)*(-2.D0*EDDY_VISCOS(I+1,J,K)*LSS13(I+1,J,K) +2.D0*EDDY_VISCOS(I-1,J,K)*LSS13(I-1,J,K))	&
		+ ETZ(I,J,K)*(-2.D0*EDDY_VISCOS(I,J+1,K)*LSS13(I,J+1,K) +2.D0*EDDY_VISCOS(I,J-1,K)*LSS13(I,J-1,K))	&
		+ ZTZ(I,J,K)*(-2.D0*EDDY_VISCOS(I,J,K+1)*LSS13(I,J,K+1) +2.D0*EDDY_VISCOS(I,J,K-1)*LSS13(I,J,K-1))	)

	SGS_Y(I,J,K) = 0.5D0*(	&
		+ XIX(I,J,K)*(-2.D0*EDDY_VISCOS(I+1,J,K)*LSS21(I+1,J,K) +2.D0*EDDY_VISCOS(I-1,J,K)*LSS21(I-1,J,K))	&
		+ ETX(I,J,K)*(-2.D0*EDDY_VISCOS(I,J+1,K)*LSS21(I,J+1,K) +2.D0*EDDY_VISCOS(I,J-1,K)*LSS21(I,J-1,K))	&
		+ ZTX(I,J,K)*(-2.D0*EDDY_VISCOS(I,J,K+1)*LSS21(I,J,K+1) +2.D0*EDDY_VISCOS(I,J,K-1)*LSS21(I,J,K-1))	&
	
		+ XIY(I,J,K)*(-2.D0*EDDY_VISCOS(I+1,J,K)*LSS22(I+1,J,K) +2.D0*EDDY_VISCOS(I-1,J,K)*LSS22(I-1,J,K))	&
		+ ETY(I,J,K)*(-2.D0*EDDY_VISCOS(I,J+1,K)*LSS22(I,J+1,K) +2.D0*EDDY_VISCOS(I,J-1,K)*LSS22(I,J-1,K))	&
		+ ZTY(I,J,K)*(-2.D0*EDDY_VISCOS(I,J,K+1)*LSS22(I,J,K+1) +2.D0*EDDY_VISCOS(I,J,K-1)*LSS22(I,J,K-1))	&
						
		+ XIZ(I,J,K)*(-2.D0*EDDY_VISCOS(I+1,J,K)*LSS23(I+1,J,K) +2.D0*EDDY_VISCOS(I-1,J,K)*LSS23(I-1,J,K))	&
		+ ETZ(I,J,K)*(-2.D0*EDDY_VISCOS(I,J+1,K)*LSS23(I,J+1,K) +2.D0*EDDY_VISCOS(I,J-1,K)*LSS23(I,J-1,K))	&
		+ ZTZ(I,J,K)*(-2.D0*EDDY_VISCOS(I,J,K+1)*LSS23(I,J,K+1) +2.D0*EDDY_VISCOS(I,J,K-1)*LSS23(I,J,K-1))	)
		          
	SGS_Z(I,J,K) = 0.5D0*(	&
		+ XIX(I,J,K)*(-2.D0*EDDY_VISCOS(I+1,J,K)*LSS31(I+1,J,K) +2.D0*EDDY_VISCOS(I-1,J,K)*LSS31(I-1,J,K))	&
		+ ETX(I,J,K)*(-2.D0*EDDY_VISCOS(I,J+1,K)*LSS31(I,J+1,K) +2.D0*EDDY_VISCOS(I,J-1,K)*LSS31(I,J-1,K))	&
		+ ZTX(I,J,K)*(-2.D0*EDDY_VISCOS(I,J,K+1)*LSS31(I,J,K+1) +2.D0*EDDY_VISCOS(I,J,K-1)*LSS31(I,J,K-1))	&

		+ XIY(I,J,K)*(-2.D0*EDDY_VISCOS(I+1,J,K)*LSS32(I+1,J,K) +2.D0*EDDY_VISCOS(I-1,J,K)*LSS32(I-1,J,K))	&
		+ ETY(I,J,K)*(-2.D0*EDDY_VISCOS(I,J+1,K)*LSS32(I,J+1,K) +2.D0*EDDY_VISCOS(I,J-1,K)*LSS32(I,J-1,K))	&
		+ ZTY(I,J,K)*(-2.D0*EDDY_VISCOS(I,J,K+1)*LSS32(I,J,K+1) +2.D0*EDDY_VISCOS(I,J,K-1)*LSS32(I,J,K-1))	&
					
		+ XIZ(I,J,K)*(-2.D0*EDDY_VISCOS(I+1,J,K)*LSS33(I+1,J,K) +2.D0*EDDY_VISCOS(I-1,J,K)*LSS33(I-1,J,K))	&
		+ ETZ(I,J,K)*(-2.D0*EDDY_VISCOS(I,J+1,K)*LSS33(I,J+1,K) +2.D0*EDDY_VISCOS(I,J-1,K)*LSS33(I,J-1,K))	&
		+ ZTZ(I,J,K)*(-2.D0*EDDY_VISCOS(I,J,K+1)*LSS33(I,J,K+1) +2.D0*EDDY_VISCOS(I,J,K-1)*LSS33(I,J,K-1))	)


	ENDDO
	ENDDO
	ENDDO
!-------------------------------------------------------------------------------------------------------------------------
!-------------------------------------------------------------------------------------------------------------------------
	!	COMPUTE P1
	DUM_IN(IS:IE,JS:JE,KS:KE) =	U(IS:IE,JS:JE,KS:KE) * T(IS:IE,JS:JE,KS:KE)
	
	CALL TEST_FILTER(DUM_IN,DUM_OUT)
	
	L_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =	- DUM_OUT(IS1:IE1,JS1:JE1,KS1:KE1)								&
									+ U_HAT(IS1:IE1,JS1:JE1,KS1:KE1)*T_HAT(IS1:IE1,JS1:JE1,KS1:KE1)
			
	!	COMPUTE R1
	DUM_IN(IS:IE,JS:JE,KS:KE) =	MAGNITUDE_LSS(IS:IE,JS:JE,KS:KE) * DTDX(IS:IE,JS:JE,KS:KE)
	
	CALL TEST_FILTER(DUM_IN,DUM_OUT)
	
	M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =																						&
		+ 4.0D0*MAGNITUDE_LSS_HAT(IS1:IE1,JS1:JE1,KS1:KE1)																&
		* 0.5D0*(XIX(IS1:IE1,JS1:JE1,KS1:KE1)*(T_HAT(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - T_HAT(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
				+ETX(IS1:IE1,JS1:JE1,KS1:KE1)*(T_HAT(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - T_HAT(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
				+ZTX(IS1:IE1,JS1:JE1,KS1:KE1)*(T_HAT(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - T_HAT(IS1:IE1,JS1:JE1,KS1-1:KE1-1))	)
	
	M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) = FILT_WID(IS1:IE1,JS1:JE1,KS1:KE1) * FILT_WID(IS1:IE1,JS1:JE1,KS1:KE1) *	&
									(M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) - DUM_OUT(IS1:IE1,JS1:JE1,KS1:KE1))

	!	COMPUTE P1*R1
	!	AND		R1*R1
	LM_IJ(IS1:IE1,JS1:JE1,KS1:KE1) = L_IJ(IS1:IE1,JS1:JE1,KS1:KE1) * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)
	
	MM_IJ(IS1:IE1,JS1:JE1,KS1:KE1) = M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)
								
	!	COMPUTE P2
	DUM_IN(IS:IE,JS:JE,KS:KE) =	V(IS:IE,JS:JE,KS:KE) * T(IS:IE,JS:JE,KS:KE)
	
	CALL TEST_FILTER(DUM_IN,DUM_OUT)
	
	L_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =	- DUM_OUT(IS1:IE1,JS1:JE1,KS1:KE1)								&
									+ V_HAT(IS1:IE1,JS1:JE1,KS1:KE1)*T_HAT(IS1:IE1,JS1:JE1,KS1:KE1)

	!	COMPUTE R2
	DUM_IN(IS:IE,JS:JE,KS:KE) =	MAGNITUDE_LSS(IS:IE,JS:JE,KS:KE)*DTDY(IS:IE,JS:JE,KS:KE)

	CALL TEST_FILTER(DUM_IN,DUM_OUT)
	
	M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =																						&
		+ 4.0D0*MAGNITUDE_LSS_HAT(IS1:IE1,JS1:JE1,KS1:KE1)																&
		* 0.5D0*(XIY(IS1:IE1,JS1:JE1,KS1:KE1)*(T_HAT(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - T_HAT(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
				+ETY(IS1:IE1,JS1:JE1,KS1:KE1)*(T_HAT(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - T_HAT(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
				+ZTY(IS1:IE1,JS1:JE1,KS1:KE1)*(T_HAT(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - T_HAT(IS1:IE1,JS1:JE1,KS1-1:KE1-1))	)

	M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =	FILT_WID(IS1:IE1,JS1:JE1,KS1:KE1) * FILT_WID(IS1:IE1,JS1:JE1,KS1:KE1) *	&
									(M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) - DUM_OUT(IS1:IE1,JS1:JE1,KS1:KE1))
			
	!	COMPUTE P1*R1 + P2*R2
	!	AND		R1*R1 + R2*R2
	LM_IJ(IS1:IE1,JS1:JE1,KS1:KE1)= LM_IJ(IS1:IE1,JS1:JE1,KS1:KE1) +	&
									L_IJ(IS1:IE1,JS1:JE1,KS1:KE1) * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)

	MM_IJ(IS1:IE1,JS1:JE1,KS1:KE1)=	MM_IJ(IS1:IE1,JS1:JE1,KS1:KE1) +	&
									M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)
								
	!	COMPUTE P3
	DUM_IN(IS:IE,JS:JE,KS:KE) =	W(IS:IE,JS:JE,KS:KE) * T(IS:IE,JS:JE,KS:KE)
	
	CALL TEST_FILTER(DUM_IN,DUM_OUT)

	L_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =	- DUM_OUT(IS1:IE1,JS1:JE1,KS1:KE1)								&
									+ W_HAT(IS1:IE1,JS1:JE1,KS1:KE1)*T_HAT(IS1:IE1,JS1:JE1,KS1:KE1)
			
	!	COMPUTE R3
	DUM_IN(IS:IE,JS:JE,KS:KE) =	MAGNITUDE_LSS(IS:IE,JS:JE,KS:KE)*DTDZ(IS:IE,JS:JE,KS:KE)

	CALL TEST_FILTER(DUM_IN,DUM_OUT)
		
	M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =							&
		+ 4.0D0*MAGNITUDE_LSS_HAT(IS1:IE1,JS1:JE1,KS1:KE1)		&
		* 0.5D0*(XIZ(IS1:IE1,JS1:JE1,KS1:KE1)*(T_HAT(IS1+1:IE1+1,JS1:JE1,KS1:KE1) - T_HAT(IS1-1:IE1-1,JS1:JE1,KS1:KE1))	&
				+ETZ(IS1:IE1,JS1:JE1,KS1:KE1)*(T_HAT(IS1:IE1,JS1+1:JE1+1,KS1:KE1) - T_HAT(IS1:IE1,JS1-1:JE1-1,KS1:KE1))	&
				+ZTZ(IS1:IE1,JS1:JE1,KS1:KE1)*(T_HAT(IS1:IE1,JS1:JE1,KS1+1:KE1+1) - T_HAT(IS1:IE1,JS1:JE1,KS1-1:KE1-1))	)

	M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) =	FILT_WID(IS1:IE1,JS1:JE1,KS1:KE1) * FILT_WID(IS1:IE1,JS1:JE1,KS1:KE1) *	&
									(M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) - DUM_OUT(IS1:IE1,JS1:JE1,KS1:KE1))
				
	!	COMPUTE P1*R1 + P2*R2 + P3*R3
	!	AND		R1*R1 + R2*R2 + R3*R3
	LM_IJ(IS1:IE1,JS1:JE1,KS1:KE1)=	LM_IJ(IS1:IE1,JS1:JE1,KS1:KE1) +	&
									L_IJ(IS1:IE1,JS1:JE1,KS1:KE1) * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)

	MM_IJ(IS1:IE1,JS1:JE1,KS1:KE1)=	MM_IJ(IS1:IE1,JS1:JE1,KS1:KE1) +	&
									M_IJ(IS1:IE1,JS1:JE1,KS1:KE1) * M_IJ(IS1:IE1,JS1:JE1,KS1:KE1)
	
	C_AVG  = 0.0D0
	LM_AVG = 0.0D0
	MM_AVG = 0.0D0
	INC    = 0.0D0
	MAX_CS_SQUARE = 0.0D0
	MIN_CS_SQUARE = 0.0D0
	
	DO K = KS1,KE1
	DO J = JS1,JE1
	DO I = IS1,IE1
		CS_SQUARE = 0.5D0 * LM_IJ(I,J,K)/MM_IJ(I,J,K)
		MAX_CS_SQUARE = MAX(MAX_CS_SQUARE,CS_SQUARE)
		MIN_CS_SQUARE = MIN(MIN_CS_SQUARE,CS_SQUARE)
		IF(CS_SQUARE.GE.0.D0) THEN
			LM_AVG = LM_AVG + LM_IJ(I,J,K)
			MM_AVG = MM_AVG + MM_IJ(I,J,K)
			INC = INC + 1.D0
		ENDIF
	ENDDO
	ENDDO
	ENDDO
	
	CALL MPI_ALLREDUCE(LM_AVG,T_LM_AVG,1,MPI_DOUBLE_PRECISION, MPI_SUM,NMCW,ERR)
	CALL MPI_ALLREDUCE(MM_AVG,T_MM_AVG,1,MPI_DOUBLE_PRECISION, MPI_SUM,NMCW,ERR)
	CALL MPI_ALLREDUCE(INC,T_INC,1,MPI_DOUBLE_PRECISION, MPI_SUM,NMCW,ERR)

	T_LM_AVG = T_LM_AVG / T_INC
	T_MM_AVG = T_MM_AVG / T_INC
	
	C_AVG = 0.5D0 * T_LM_AVG/T_MM_AVG
	
	IF(T_MM_AVG.LT.1D-6)	C_AVG = 0.D0
	
	IF(RANK.EQ.0) THEN
		OPEN(UNIT=13,FILE='CT.PLT',STATUS='OLD',ACCESS='APPEND')
		WRITE(13,*) RTIME,DSQRT(C_AVG)
		CLOSE(13)
	ENDIF
		
	EDDY_CONDUC(IS:IE,JS:JE,KS:KE) = C_AVG*FILT_WID(IS:IE,JS:JE,KS:KE)**2*MAGNITUDE_LSS(IS:IE,JS:JE,KS:KE)
	
	!	DIVERSENCE OF SUBGRID-SCALE TEMPERATURE FLUXES
	DO K=KS1,KE1
	DO J=JS1,JE1
	DO I=IS1,IE1

		SGS_T(I,J,K) = 0.5D0*(	&
		+ XIX(I,J,K)*(-2.D0*EDDY_CONDUC(I+1,J,K)*DTDX(I+1,J,K) +2.D0*EDDY_CONDUC(I-1,J,K)*DTDX(I-1,J,K))	&
		+ ETX(I,J,K)*(-2.D0*EDDY_CONDUC(I,J+1,K)*DTDX(I,J+1,K) +2.D0*EDDY_CONDUC(I,J-1,K)*DTDX(I,J-1,K))	&
		+ ZTX(I,J,K)*(-2.D0*EDDY_CONDUC(I,J,K+1)*DTDX(I,J,K+1) +2.D0*EDDY_CONDUC(I,J,K-1)*DTDX(I,J,K-1))	&

		+ XIY(I,J,K)*(-2.D0*EDDY_CONDUC(I+1,J,K)*DTDY(I+1,J,K) +2.D0*EDDY_CONDUC(I-1,J,K)*DTDY(I-1,J,K))	&
		+ ETY(I,J,K)*(-2.D0*EDDY_CONDUC(I,J+1,K)*DTDY(I,J+1,K) +2.D0*EDDY_CONDUC(I,J-1,K)*DTDY(I,J-1,K))	&
		+ ZTY(I,J,K)*(-2.D0*EDDY_CONDUC(I,J,K+1)*DTDY(I,J,K+1) +2.D0*EDDY_CONDUC(I,J,K-1)*DTDY(I,J,K-1))	&
					
		+ XIZ(I,J,K)*(-2.D0*EDDY_CONDUC(I+1,J,K)*DTDZ(I+1,J,K) +2.D0*EDDY_CONDUC(I-1,J,K)*DTDZ(I-1,J,K))	&
		+ ETZ(I,J,K)*(-2.D0*EDDY_CONDUC(I,J+1,K)*DTDZ(I,J+1,K) +2.D0*EDDY_CONDUC(I,J-1,K)*DTDZ(I,J-1,K))	&
		+ ZTZ(I,J,K)*(-2.D0*EDDY_CONDUC(I,J,K+1)*DTDZ(I,J,K+1) +2.D0*EDDY_CONDUC(I,J,K-1)*DTDZ(I,J,K-1))	)
	
	ENDDO
	ENDDO
	ENDDO
!-------------------------------------------------------------------------------------------------------------------------
	END SUBROUTINE STRESS_TENSOR_DY